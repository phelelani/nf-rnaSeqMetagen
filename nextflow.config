// Configuration File
manifest {
    author = 'Phelelani Mpangase'
    homePage = 'https://github.com/phelelani/nf-rnaSeqMetagen'
    description = 'rnaSeqMetagen pipeline'
    mainScript = 'main.nf'
    version = '0.2'
}

// CHECK INPUTS AND OUTPUTS:
params {
    data       = null
    out        = null
    db         = null
    genome     = null
    genes      = null
    help       = null
    mode       = null
    
    // RESOURCES
    max_memory = 200.GB
    max_cpus   = 24
    max_time   = 24.h
}

//  FUNCTION TO ENSURE THAT RESOURCE REQUIREMENTS DON'T GO BEYOND A MAXIMUM LIMIT
def check_max(obj, type) {
    if(type == 'memory'){
        try {
            if(obj.compareTo(params.max_memory as nextflow.util.MemoryUnit) == 1)
            return params.max_memory as nextflow.util.MemoryUnit
            else
            return obj
        } catch (all) {
            println "   ### ERROR ###   Max memory '${params.max_memory}' is not valid! Using default value: $obj"
            return obj
        }
    } else if(type == 'time'){
        try {
            if(obj.compareTo(params.max_time as nextflow.util.Duration) == 1)
            return params.max_time as nextflow.util.Duration
            else
            return obj
        } catch (all) {
            println "   ### ERROR ###   Max time '${params.max_time}' is not valid! Using default value: $obj"
            return obj
        }
    } else if(type == 'cpus'){
        try {
            return Math.min( obj, params.max_cpus as int )
        } catch (all) {
            println "   ### ERROR ###   Max cpus '${params.max_cpus}' is not valid! Using default value: $obj"
            return obj
        }
    }
}


// CHECK INPUT AND OUTPUT PATHS
def checkPaths() {
    def bind_dirs = []

    // CHECK DATA INPUT DIR
    if(params.data == null) {
        // DO NOTHING!
    } else{
        data_dir = new File("${params.data}")
        bind_dirs.add("${data_dir}")
    }

    // CHECK DATA OUTPUT DIR
    if(params.out == null) {
        out_dir = new File("$PWD/results_nf-rnaSeqMetagen")
    } else{
        out_dir = new File("${params.out}")
        bind_dirs.add("${out_dir}")
    }

    // CHECK KRAKEN2_DB
    if(params.db == null) {
        // DO NOTHING!
    } else{
        db = new File("${params.db}")
        bind_dirs.add(db)
    }

    // CHECK GENOME
    if(params.genome == null) {
        // DO NOTHING!
    } else{
        genome_dir = new File("${params.genome}").getParent()
        bind_dirs.add("${genome_dir}")
    }
    
    // CHECK GENES
    if(params.genes == null) {
        // DO NOTHING!
    } else{
        genes_dir = new File("${params.genes}").getParent()
        bind_dirs.add("${genes_dir}")
    }

    // BIND GENOME AND GENES DIR
    if (bind_dirs.isEmpty()) {
        dirs = " "
    } else {
        dirs = bind_dirs.unique().collect { it -> "-B ${it}"}.join(" ").toString()
    }
    return [ "${dirs}", out_dir ]
}

// SINGULARITY OPTIONS
def singularityOptions() {
    singularity {
        enabled = true
        cacheDir = "$HOME/.singularity/cache"
        runOptions = "$dirs" + " --cleanenv"
        autoMounts = true
    }
}

// PIPELINE TRACING, VISUALIZATION AND HELP
def pipelineHelp() {
    if(params.help || params.mode == null) {
    } else if(params.mode == 'run.FilterClassify') {
        trace {
            enabled = true
            overwrite = true
            file = "${out_dir}/workflow-tracing/nf-rnaSeqMetagen_trace.txt"
        }
        timeline {
            enabled = true
            overwrite = true
            file = "${out_dir}/workflow-tracing/nf-rnaSeqMetagen_timeline.html"
        }
        report {
            enabled = true
            overwrite = true
            file = "${out_dir}/workflow-tracing/nf-rnaSeqMetagen_report.html"
        }
        dag {
            enabled = true
            overwrite = true
            file = "${out_dir}/workflow-tracing/nf-rnaSeqMetagen_flow.dot"
        }
    } else{}
}

// ALL PROCESSESS SETTINGS
process {
    // ALL PROCESSES
    cache = true
    stageInMode = 'symlink'
    stageOutMode = 'rsync'

    // SPECIAL RESOURCE REQUIREMENTS
    withLabel: 'mini' {
        cpus = 1 //{ check_max( 1 * task.attempt, 'cpus' ) }
        memory = 4.GB //{ check_max( 4.GB * task.attempt, 'memory' ) }
        time = 72.h //{ check_max( 72.h * task.attempt, 'time' ) }
    }   
    withLabel: 'midi' {
        cpus = 6 //{ check_max( 8 * task.attempt, 'cpus' ) }
        memory = 24.GB //{ check_max( 24.GB * task.attempt, 'memory' ) }
        time = 72.h //{ check_max( 72.h * task.attempt, 'time' ) }
    }   
    withLabel: 'maxi' {
        cpus = 12 //{ check_max( 16 * task.attempt, 'cpus' ) }
        memory = 56.GB //{ check_max( 56.GB * task.attempt, 'memory' ) }
        time = 72.h // { check_max( 72.h * task.attempt, 'time' ) }
    }

    // CONTAINERS
    withName:'run_GenerateSTARIndex|run_STAR' {
        container = 'docker://phelelani/nf-rnaseqmetagen:star'
    }
    withName:'run_TrinityAssemble' {
        container = 'docker://phelelani/nf-rnaseqmetagen:trinity'
        cpus = 20
        memory = 150.GB
    }
    withName:'run_DownloadK2DBLibs|run_BuildK2DB|run_UpdateTaxonomy|run_KrakenClassifyReads|run_KrakenClassifyFasta|run_KronaReport' {
        container = 'docker://phelelani/nf-rnaseqmetagen:kraken2'
    }
    withName:'run_CollectTaxSeqs|run_CopyUpsetDir|run_PrepareMatrixData|run_CreateMatrix' {
        container = 'docker://phelelani/nf-rnaseqmetagen:upset'
    }
    withName:'run_MultiQC' {
        container = 'docker://phelelani/nf-rnaseqmetagen:multiqc'
    }
}

// PROFILES AND PROCESS OPTIONS
profiles {
    standard {
        executor.name = 'local'
        checkPaths()
        singularityOptions()
        pipelineHelp()
    }
    slurm {
        executor.name = 'slurm'
        queue = 'batch'
        checkPaths()
        singularityOptions()
        pipelineHelp()
    }
}
